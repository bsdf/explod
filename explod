#!/usr/bin/env perl

use strict;
use warnings;

use File::Find;

my $arg = shift @ARGV;

undo() if $arg and $arg eq "--undo";
usage() if not $arg or not -d $arg;

find(
    {
        wanted   => \&match,
        no_chdir => 1,
    },
    $arg
);

sub match {
    return if -d or not /\.pm$/;
    my $orig = $_;

    s/lib\///;
    symlink $orig, join "::", split /\//;
}

sub undo {
    for (<*.pm>) {
        unlink $_ if -l;
    }
    exit;
}

sub usage {
    print <<USAGE;
usage:  explod Path/To/Module
            creates symlinks to pm files in path.

        explod --undo
            removes created symlinks
USAGE

    exit;
}
